import rolando.*
describe "Test Rolando Entrega 1 - Punto 1: Hechiceria"{
	test "Conocer el nivel de hechiceria de Rolando" {
		assert.equals(56, rolando.nivelHechiceria())
	}
	test "Cambio de nombre de hechizo y nuevo nivel de hechiceria" {
		espectroMalefico.nombre("espectro superrecontramalefico muajajaja")
		assert.equals(125, rolando.nivelHechiceria())
	}
	test "Cambiar hechizo preferido y nuevo nivel de hechiceria" {
		rolando.hechizoPreferido(hechizoBasico)
		assert.equals(35, rolando.nivelHechiceria())
	}
	test "Ocurre Eclipse, Conocer nivel de hechiceria" {
		fuerzaOscura.eclipse()
		assert.equals(61, rolando.nivelHechiceria())
	}	
	test "Saber si espectro malefico es poderoso" {
		assert.that(espectroMalefico.esPoderoso())
	}	
	test "Cambiar nombre de espectro malefico por pepe y saber si es poderoso" {
		espectroMalefico.nombre("pepe")
		assert.notThat(espectroMalefico.esPoderoso())
	}	
	test "Saber si hechizo basico es poderoso" {
		assert.notThat(hechizoBasico.esPoderoso())
	}	
}

describe "Test Rolando Entrega 1 - Punto 2: Lucha"{
	method initialize(){
		rolando.agregarArtefacto(espadaDelDestino)
		rolando.agregarArtefacto(collarDivino)
		rolando.agregarArtefacto(mascaraOscura)
	}
	method nuevoArtefacto(unidadesDeLucha) = 
		object{
			method unidadesDeLucha(personaje) = unidadesDeLucha
			}
	test "Eliminar todos los artefactos de Rolando y conocer su nivel de lucha" {
		rolando.removerArtefacto(espadaDelDestino)
		rolando.removerArtefacto(collarDivino)
		rolando.removerArtefacto(mascaraOscura)
		assert.equals(1, rolando.valorDeLucha())
	}
	test "Conocer la habilidad para la lucha de Rolando con todos los artefactos" {
		assert.equals(13, rolando.valorDeLucha())
	}
	test "Cambiar las perlas a 3 y conocer el nuevo nivel de lucha" {
		collarDivino.cantidadDePerlas(3)
		assert.equals(11, rolando.valorDeLucha())
	}
	test "Cambiar el valor base de lucha a 8 y conocer el nuevo nivel de lucha" {
		rolando.nivelLuchaBase(8)
		assert.equals(20, rolando.valorDeLucha())
	}	
	test "Hacer que ocurra un eclipse, nuevo valor de lucha de la mascara" {
		fuerzaOscura.eclipse()
		assert.equals(5, mascaraOscura.unidadesDeLucha(rolando))
	}	
	test "Eliminar la mascara oscura de los artefactos y conocer su nuevo nivel de lucha" {
		rolando.removerArtefacto(mascaraOscura)
		assert.equals(9,rolando.valorDeLucha())
	}	
	test "Valor de lucha de Rolando con artefacto de 1000 unidades de lucha" {
		const nuevoArtefacto = self.nuevoArtefacto(1000)
		rolando.agregarArtefacto(nuevoArtefacto)
		assert.equals(1013, rolando.valorDeLucha())
	}	
}
